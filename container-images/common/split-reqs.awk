# Split a requirements.txt file by the index the dependency was resolved from.
# The file must have been generated by uv with the --emit-index-annotation option.

BEGIN {
    lines = ""
}
{ lines = lines $0 ORS }
/^    # from / {
    split($3, parts, "/")
    filename = gensub(/\.txt$/, "-" parts[3] ".txt", 1, FILENAME)
    if (!(filename in seen)) {
        print "Creating " filename
        print "# This file is generated from " FILENAME " by split-reqs.awk, do not modify manually." > filename
        print "# Includes dependencies retrieved from " $NF >> filename
        print "" >> filename
        print "--index-url " $NF >> filename
        print "" >> filename
        seen[filename] = 1
    }
    printf "%s", lines >> filename
    lines = ""
}
